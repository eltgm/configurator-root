plugins {
    id 'org.springframework.boot' version '3.4.11'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'org.openapi.generator' version '7.11.0'
    id 'org.jooq.jooq-codegen-gradle' version "3.20.9"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation "org.flywaydb:flyway-core:11.17.0"
    runtimeOnly "org.flywaydb:flyway-database-postgresql:11.17.0"
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.postgresql:postgresql:42.7.3'

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    implementation 'org.mapstruct:mapstruct:1.6.0'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.0'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-core:2.4-M4-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.4-M4-groovy-4.0'

    testImplementation 'org.testcontainers:postgresql:1.20.2'
    testImplementation 'org.testcontainers:junit-jupiter:1.20.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/specs/configurator-api.yaml".toString()
    outputDir = "$buildDir/generated/openapi".toString()
    apiPackage = 'ru.sultanyarov.configurator.api.inbounds.rest'
    modelPackage = 'ru.sultanyarov.configurator.domain.dto'
    invokerPackage = 'ru.sultanyarov.configurator.api'
    configOptions = [
            dateLibrary         : "java8",
            skipDefaultInterface: "true",
            interfaceOnly       : "true",
            oas3                : "true",
            useSpringController : "true",
            useSpringfox        : "false",
            useTags             : "true",
            useSpringBoot3      : "true"
    ]
}

tasks.named('compileJava') {
    dependsOn tasks.named('openApiGenerate')
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/openapi/src/main/java"
        }
    }
}

jooq {
    configuration {
        jdbc {
            url = System.getenv('DB_URL') ?: 'jdbc:postgresql://localhost:5432/configurator'
            user = System.getenv('DB_USER') ?: 'configurator'
            password = System.getenv('DB_PASSWORD') ?: 'configurator'
            driver = 'org.postgresql.Driver'
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
                includes = '.*'
            }
            generate {
                daos = false
                pojos = true
            }
            target {
                packageName = 'ru.sultanyarov.configurator.domain.entity.jooq'
                directory = "$buildDir/generated/jooq".toString()
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/jooq"
        }
    }
}
